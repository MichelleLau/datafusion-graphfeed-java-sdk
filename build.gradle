group "com.thomsonreuters.datafusion"
version file('version.txt').text.trim()

apply plugin: "application"
apply plugin: "java"
apply plugin: "groovy"
apply plugin: "idea"
apply plugin: "project-report"
apply plugin: 'maven'
apply plugin: 'signing'

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.7'
    compile 'org.slf4j:slf4j-api:1.7.21'
    compile 'ch.qos.logback:logback-classic:1.1.7'
    compile 'commons-io:commons-io:2.5'

    compile 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'

    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
    testCompile 'cglib:cglib-nodep:2.2.2'
    testCompile 'org.apache.commons:commons-compress:1.12'
}

sourceCompatibility = 1.8

task wrapper(type: Wrapper) {
    gradleVersion = 3.2
}

task javadocJar(type: Jar, dependsOn: groovydoc) {
    classifier = 'javadoc'
    from 'build/docs/groovydoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

ext.ossrhUsernameValue = project.hasProperty("ossrhUsername") ? project.getProperty("ossrhUsername") : ""
ext.ossrhPasswordValue = project.hasProperty("ossrhPassword") ? project.getProperty("ossrhPassword") : ""

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsernameValue, password: ossrhPasswordValue)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsernameValue, password: ossrhPasswordValue)
            }

            pom.project {
                name 'GraphFeed SDK'
                packaging 'jar'
                description 'GraphFeed Java SDK provides an easy way to get started with the GraphFeed API'
                url 'https://github.com/thomsonreuters/datafusion-graphfeed-java-sdk'

                scm {
                    url 'scm:git@github.com:thomsonreuters/datafusion-graphfeed-java-sdk.git'
                    connection 'scm:git@github.com:thomsonreuters/datafusion-graphfeed-java-sdk.git'
                    developerConnection 'scm:git@github.com:thomsonreuters/datafusion-graphfeed-java-sdk.git'
                }

                licenses {
                    license {
                        name 'The MIT License (MIT)'
                        url 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id 'obildtsen'
                        name 'Ola Bildtsen'
                    }
                    developer {
                        id 'bobbywarner'
                        name 'Bobby Warner'
                    }
                    developer {
                        id 'mjhugo'
                        name 'Mike Hugo'
                    }
                }
            }
        }
    }
}
